10 ' *********************************
20 '
30 '   INDIANA SHONES PART 1
40 ' 
50 '   PROGRAMMED BY ABURI6800 2023
60 ' 
70 ' *********************************

100 SCREEN 1,2,0:WIDTH 32:COLOR 15,1,1:KEY OFF:DEFINT A-Z:CLEAR 200,&HC000:DEFUSR=&H7E:A=USR(0)
110 GOSUB 9000:GOTO 300

150 ' ***** PLAYER SPRITE PUT *****
160 PUT SPRITE 0,(X(0)\16,Y(0)\16),4,(P(0)+R(0)*2)
170 PUT SPRITE 1,(X(0)\16,Y(0)\16),11,(P(0)+R(0)*2)+1:RETURN

200 ' ***** PLAYER SPRITE UPDATE *****
210 X=X(0)\16:Y=Y(0)\16:P=(P(0)*4)+(R(0)*8):AD=BASE(8)
220 VPOKE AD,Y:VPOKE AD+1,X:VPOKE AD+2,P
230 VPOKE AD+4,Y:VPOKE AD+5,X:VPOKE AD+6,P+4:RETURN

250 ' ***** OTHER SPRITE PUT *****
260 PUT SPRITE N+1,(X(N)\16,Y(N)\16),C(N),P(N):RETURN

270 ' ***** OTHER SPRITE UPDATE *****
280 AD=BASE(8)+((N+1)*4):P=(P(N)+R(N))*4
290 VPOKE AD,Y(N)\16:VPOKE AD+1,X(N)\16:VPOKE AD+2,P:RETURN

300 ' ***** MAIN ROUTINE *****
310 ON GS GOSUB 5000,4500,400
320 GOTO 300

400 ' ***** GAME MAIN *****
410 ON BF+1 GOSUB 500,600
420 IF SF THEN GOSUB 800
430 GOSUB 1000
440 A=USR2(0):RETURN

500 ' ***** PLAYER CONTROL *****
510 IF JF THEN Y(0)=Y(0)+JY(JF):JF=JF-1:S=JS:GOTO 540
520 IF STRIG(0)+STRIG(1) AND BF=0 AND JF=0 THEN BF=1:RETURN
530 S=STICK(0)+STICK(1):IF S=8 OR S=1 OR S=2 THEN JF=7:JS=S:RETURN
540 X(0)=X(0)+VX(S):IF X(0)<512 OR X(0)>3328 THEN X(0)=X(0)-VX(S)
550 IF S=0 THEN R(0)=1:GOTO 590
560 IF (S=6 OR S=7 OR S=8) AND P(0)<>4 THEN P(0)=4:R(0)=0:GOTO 590
570 IF (S=2 OR S=3 OR S=4) AND P(0)<>0 THEN P(0)=0:R(0)=0:GOTO 590
580 R(0)=R(0) XOR 1
590 GOSUB 200:RETURN

600 ' ***** SITE CONTROL *****
610 IF STRIG(0)+STRIG(1)=0 AND SF=0 GOTO 700
620 S=STICK(0)+STICK(1)
625 X(1)=X(1)+VX(S):Y(1)=Y(1)+VY(S)
630 IF X(1)<512 OR X(1)>3328 THEN X(1)=X(1)-VX(S)
640 IF Y(1)<0 OR Y(1)>1664 THEN Y(1)=Y(1)-VY(S)
650 IF S>0 THEN N=1:GOSUB 270
660 RETURN

700 ' ***** KNIFE INIT *****
710 BF=0:SF=1:N=2:X=X(0):Y=Y(0):TX=X(1):TY=Y(1):SP=2:GOSUB 2000
720 P(N)=11:F(N)=1:R(N)=0:C(N)=15:GOSUB 250:RETURN

800 ' ***** KNIFE MOVE *****
810 N=2:GOSUB 2100:IF TF=0 THEN RETURN
820 FOR I=4 TO 5
830 IF ABS(X(N)-X(I))<256 AND ABS(Y(N)-Y(I))<256 THEN SC=SC+50+(F(I)=2)*50:PLAY "T255L32O5V12CGEB":F(I)=0:Y(I)=3088:SWAP N,I:GOSUB 250:SWAP N,I
840 NEXT I:Y(N)=3088:GOSUB 250:SF=0:RETURN

1000 ' ***** ENEMY MOVE *****
1010 N=C+3:ON C GOSUB 1100,1100,1500
1020 C=C+1:IF C>3 THEN C=1
1030 RETURN

1100 ' ----- BAT -----
1110 ON F(N)+1 GOTO 1120,1150,1300

1120 IF INT(RND(1)*3)<>0 THEN RETURN
1130 X=(INT(RND(1)*22)+4)*8*16:Y=0:TX=(INT(RND(1)*22)+4)*8*16:TY=(INT(RND(1)*3)+2)*8*16:SP=1:GOSUB 2000
1140 BEEP:F(N)=1:P(N)=12:R(N)=0:C(N)=5:GOSUB 250:RETURN

1150 GOSUB 2100:IF TF THEN 1200
1160 R(N)=R(N) XOR 1:GOSUB 270
1170 IF F(N)=1 AND INT(RND(1)*5)=0 THEN LOCATE X(N)\128+INT(RND(1)*2),Y(N)\128+1:PRINT "n";
1180 RETURN

1200 X=X(N):Y=Y(N):IF INT(RND(1)*10)+1<RD*2 THEN 1220
1210 TX=(INT(RND(1)*22)+4)*8*16:TY=(INT(RND(1)*5))*8*16:SP=1:GOSUB 2000:RETURN
1220 TX=X(0):TY=Y(0)+(8*16):SP=2:GOSUB 2000:F(N)=2:RETURN

1300 GOSUB 2100:IF TF=0 THEN 1160
1310 X=X(N):Y=Y(N):TX=X(N):TY=(INT(RND(1)*3)+2)*8*16:SP=1:GOSUB 2000:F(N)=1:RETURN

1500 ' ----- SKELETON -----
1510 ON F(N)+1 GOTO 1520,1550

1520 IF INT(RND(1)*5)<>0 THEN RETURN
1530 F(N)=1:C(N)=14:R(N)=0:SP=1:IF X(0)>2048 THEN X=0:TX=3968:P(N)=14 ELSE X=3968:TX=0:P(N)=16
1540 Y=2304:TY=2304:GOSUB 2000:RETURN

1550 GOSUB 2100:IF TF THEN F(N)=0:Y(N)=3088:GOSUB 250:RETURN
1560 R(N)=R(N) XOR 1:GOSUB 270:RETURN

2000 ' ***** CHARACTER MOVE INIT SUB *****
2010 X(N)=X:Y(N)=Y:TX(N)=TX:TY(N)=TY:DX=TX-X(N):DY=TY-Y(N)
2020 IF ABS(DX)>ABS(DY) THEN A=ABS(DX) ELSE A=ABS(DY)
2030 WX(N)=(DX/A)*16*(8*SP):WY(N)=(DY/A)*16*(8*SP)
2040 RETURN

2100 ' ***** CHARACTER MOVE SUB *****
2110 X(N)=X(N)+WX(N):Y(N)=Y(N)+WY(N):GOSUB 270
2120 TF=0:IF ABS(X(N)-TX(N))<256 AND ABS(Y(N)-TY(N))<256 THEN TF=1
2130 RETURN

4500 ' ***** GAME INIT *****
4510 RD=1:LF=3:SC=0:SF=0:BF=0:JF=0:JS=0:FOR I=0 TO 8:F(I)=0:NEXT I
4520 X(0)=15*8*16:Y(0)=18*8*16:P(0)=0:F(0)=1:GOSUB 150
4530 X(1)=15*8*16:Y(1)=10*8*16:P(1)=10:F(1)=1:C(1)=14:N=1:GOSUB 250
4540 FOR I=3 TO 5:F(I)=0:NEXT I
4550 GS=3:C=1:GOSUB 8000:RETURN

5000 ' ***** TITLE *****
5010 CLS:GOSUB 8000
5020 FOR I=0 TO 6
5030 VPOKE &H184B+I,161+I:VPOKE &H186B+I,193+I
5050 VPOKE &H188E+I,168+I:VPOKE &H18AE+I,200+I
5060 NEXT I
5070 FOR I=0 TO 15:VPOKE &H18E8+I,175+I:NEXT I
5080 LOCATE 8,16:PRINT "[ABURI GAMES 2023";
5090 LOCATE 7,18:PRINT "ALL RIGHTS RESERVED";
5100 X(0)=2*8*16:Y(0)=18*8*16:F(0)=1:P(0)=0:R(0)=1:GOSUB 150
5110 GOSUB 8800:LOCATE 11,12:PRINT "PUSH BUTTON";
5120 X(1)=5:X(2)=8:X(3)=21:X(4)=26:X(5)=27
5130 Y(1)=3:Y(2)=1:Y(3)=2:Y(4)=1:Y(5)=3
5140 FOR I=1 TO 5:F(I)=RND(1)*2:NEXT I:N=1
5150 GOSUB 5300:IF STRIG(0)+STRIG(1)=0 THEN 5150
5160 FOR I=12 TO 18:LOCATE 7,I:PRINT "                   ";:NEXT I
5170 X1=3:X2=26:GOSUB 5200:GOSUB 6030:X1=26:X2=30:GOSUB 5200:Y(0)=3088:GOSUB 150
5180 GS=2:RETURN
5200 ' ----- SHONES MOVE ----
5210 FOR I=X1 TO X2:X(0)=I*8*16:R(0)=R(0) XOR 1:GOSUB 200:FOR W=0 TO 79:NEXT W:NEXT I
5300 ' ----- EYES -----
5310 IF INT(RND(1)*8)=0 THEN F(N)=F(N) XOR 1:LOCATE X(N),Y(N):PRINT MID$(" m",F(N)+1,1);
5320 N=N+1:IF N>5 THEN N=1
5330 RETURN

6000 ' ***** DOOR ANIM *****
6010 X=0:GOTO 6050 :' LEFT OPEN
6020 X=0:GOTO 6060 :' LEFT CLOSE
6030 X=30:GOTO 6050 :' RIGHT OPEN
6040 X=30:GOTO 6060 :' RIGHT CLOSE
6050 Y=2:GOSUB 6110:Y=1:GOSUB 6120:Y=0:GOSUB 6130:RETURN :' OPEN
6060 Y=0:GOSUB 6130:Y=1:GOSUB 6120:Y=2:GOSUB 6110:RETURN :' CLOSE
6100 ' ---- DOOR PRINT SUB -----
6110 LOCATE X,17+Y-2:PRINT "of";
6120 LOCATE X,17+Y-1:PRINT "fo";
6130 LOCATE X,17+Y:PRINT "oo";
6140 IF Y<2 THEN LOCATE X,17+Y+1:PRINT "  ";
6150 GOSUB 6200:RETURN
6200 ' ----- DOOR MOVE SOUND SUB -----
6210 FOR Q=2 TO 0 STEP -1:FOR R=100 TO 0 STEP -20
6220 SOUND 4,Q*60+R+30:SOUND 5,14:SOUND 10,7+(Q*3)+(r\20)
6230 NEXT R,Q:SOUND 10,0:RETURN

8000 ' ***** COMMON SCREEN MAKE *****
8010 LOCATE 0, 0:PRINT "faaf                        aafa";
8020 LOCATE 0, 1:PRINT "aafd                        ceba";
8030 LOCATE 0, 2:PRINT "bff                          aaf";
8040 LOCATE 0, 3:PRINT "fac                          daf";
8050 LOCATE 0, 4:PRINT "ad                            fa";
8060 LOCATE 0, 5:PRINT "a d                           ca";
8070 LOCATE 0, 6:PRINT "a                            e a";
8080 LOCATE 0, 7:PRINT "d                              e";
8090 LOCATE 0, 8:PRINT " d                            e ";
8100 LOCATE 0, 9:PRINT "b                               ";
8110 LOCATE 0,10:PRINT "f                              b";
8110 LOCATE 0,11:PRINT "bd                            bf";
8120 LOCATE 0,12:PRINT "d                             eb";
8130 LOCATE 0,13:PRINT "                               e";
8140 LOCATE 0,15:PRINT "                              hh";
8150 LOCATE 0,16:PRINT "                             hhh";
8160 LOCATE 0,17:PRINT "                             hoo";
8170 LOCATE 0,18:PRINT "                             hof";
8180 LOCATE 0,19:PRINT "                             hfo";
8190 LOCATE 0,20:PRINT "gggggggggggggggggggggggggggggggg";
8200 LOCATE 0,22:PRINT " SCORE        STAGE      LEFT";
8210 IF GS=1 THEN RETURN
8230 LOCATE 0,15:PRINT "hh";
8240 LOCATE 0,16:PRINT "hhh";
8250 LOCATE 0,17:PRINT "ofh";
8260 LOCATE 0,18:PRINT "foh";
8270 LOCATE 0,19:PRINT "ooh";
8280 RETURN

8800 ' ***** MAIN THEME *****
8810 PLAY "T110V12L16O4ER16FGR16","T110V12L16O3R8R16CR16","T110V12O4L16CV6CV12DEV6E"
8820 PLAY "O5CC8C4O4DR16EFF2","O4CO3CR16O4CO3CR16O4CO3CR16O4CO2GR16O3BO2GR16O3BO2GR16O3B","V12GG8G4O3BV6BV12AO4DD2"
8830 PLAY "GR16ABR16O5EE8E4","O2GR16O3BO2GR16O3BO2GR16O3BO2GR16O3B","EV6EV12FGV6GV12BB8B4"
8840 PLAY "O4AR16BO5CC2","O2AR16O3BO3CC2","EV6EV12FGG2"
8850 IF PLAY(0) THEN 8850 ELSE RETURN

9000 ' ***** INITIALIZE *****
9050 DIM X(8),Y(8),TX(8),TY(8),F(8),C(8),WX(8),WY(8),VX(8),VY(8),JY(8):C=32*17::GS=1
9060 FOR I=1 TO 8:READ VX(I),VY(I):NEXT I
9070 FOR I=1 TO 7:READ JY(I):NEXT I
9100 ' PATTERN GENERATE TABLE & COLOR TABLE & SPRITE PATTERN
9110 BLOAD "PROGRAM.BIN":DEFUSR1=&HC000:DEFUSR2=&HC100:A=USR1(0)
9120 RETURN

9500 ' ***** DATA *****
9510 DATA    0,-128,  128,-128,  128,   0,  128, 128,    0,  128, -128, 128, -128,   0, -128,-128
9520 DATA 192, 128, 64, 0, -64, -128, -192
