10 ' *********************************
20 '
30 '   INDIANA SHONES PART 1
40 ' 
50 '   PROGRAMMED BY ABURI6800 2023
60 ' 
70 ' *********************************

100 GOSUB 9000

200 ' ***** GAME INIT *****
210 MX=40*&HF:MY=40*&HF:NX=190*&HF:NY=120*&HF:SP=4:BF=0:SF=0
220 PUT SPRITE 0,(MX\&HF,MY\&HF),15,0
230 PUT SPRITE 1,(NX\&HF,NY\&HF),11,1

300 ' ***** MAIN ROUTINE *****
310 GOSUB 400
320 GOTO 300

400 ' ***** GAME MAIN *****
410 ON BF+1 GOSUB 500,600
420 IF SF THEN GOSUB 800
430 RETURN

500 ' ***** PLAYER CONTROL *****
510 IF STRIG(0)+STRIG(1) THEN BF=1:RETURN
520 S=STICK(0)+STICK(1):MX=MX+VX(S):MY=MY+VY(S)
530 IF MX<0 OR MX>3824 THEN MX=MX-VX(S)
540 IF MY<0 OR MY>2800 THEN MY=MY-VY(S)
550 PUT SPRITE 0,(MX\&HF,MY\&HF),15,0
560 RETURN

600 ' ***** SITE CONTROL *****
610 IF STRIG(0)+STRIG(1)=0 GOTO 700
620 S=STICK(0)+STICK(1):NX=NX+VX(S):NY=NY+VY(S)
630 IF NX<0 OR NX>3824 THEN NX=NX-VX(S)
640 IF NY<0 OR NY>2800 THEN NY=NY-VY(S)
650 PUT SPRITE 1,(NX\&HF,NY\&HF),11,1
660 RETURN

700 ' ***** SHOT INIT *****
710 BF=0:SF=1:BX=MX:BY=MY:DX=NX-MX:DY=NY-MY
720 IF ABS(DX)>ABS(DY) THEN A=ABS(DX) ELSE A=ABS(DY)
730 WX=(DX/A)*&HF*SP:WY=(DY/A)*&HF*SP
740 RETURN

800 ' ***** SHOT MOVE *****
810 BX=BX+WX:BY=BY+WY
820 PUT SPRITE 2,(BX\&HF,BY\&HF),10,2
830 IF ABS(BX-NX)<8*&HF AND ABS(BY-NY)<8*&HF THEN SF=0
840 RETURN

9000 ' ***** INITIALIZE *****
9010 SCREEN 1,1,0:WIDTH 32:COLOR 15,1,1:KEY OFF:DEFINT A-Z
9020 SPRITE$(0)=CHR$(&HFF)+CHR$(&HFF)+CHR$(&HFF)+CHR$(&HFF)+CHR$(&HFF)+CHR$(&HFF)+CHR$(&HFF)+CHR$(&HFF)
9030 SPRITE$(1)=CHR$(&HC3)+CHR$(&H81)+CHR$(&H00)+CHR$(&H00)+CHR$(&H00)+CHR$(&H00)+CHR$(&H81)+CHR$(&HC3)
9040 SPRITE$(2)=CHR$(&H00)+CHR$(&H00)+CHR$(&H18)+CHR$(&H2C)+CHR$(&H3C)+CHR$(&H18)+CHR$(&H00)+CHR$(&H00)
9050 DIM VX(8),VY(8)
9060 FOR I=1 TO 8:READ VX(I),VY(I):NEXT I
9070 RETURN
10000 ' ***** DATA *****
10100 DATA   0,-96,  96,-96,  96,  0,  96, 96,   0, 96, -96, 96, -96,  0, -96,-96
